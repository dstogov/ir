--TEST--
DESSA 005 (swap & tmp register)
--ARGS--
--emit-c
--CODE--
{
	uintptr_t c_1 = 0;
	bool c_2 = 0;
	bool c_3 = 1;
	uintptr_t c_4 = func printf(uintptr_t, ...): void;
	uintptr_t c_5 = "%g %g %g %g %g\n";
	l_1 = START(l_21);
	double d_2 = PARAM(l_1, "arg_1", 1);
	double d_3 = PARAM(l_1, "arg_2", 2);
	double d_4 = PARAM(l_1, "arg_3", 3);
	double d_5 = PARAM(l_1, "arg_4", 4);
	double d_6 = PARAM(l_1, "arg_5", 5);
	bool d_7 = PARAM(l_1, "cond", 6);
	l_8 = END(l_1);
	l_9 = LOOP_BEGIN(l_8, l_16);
	double d_10 = PHI(l_9, d_3, d_2);
	double d_11 = PHI(l_9, d_4, d_10);
	double d_12 = PHI(l_9, d_5, d_13);
	double d_13 = PHI(l_9, d_6, d_12);
	l_14 = IF(l_9, d_7);
	l_15 = IF_TRUE(l_14);
	l_16 = LOOP_END(l_15);
	l_17 = IF_FALSE(l_14);
	l_18 = CALL/6(l_17, c_4, c_5, d_2, d_10, d_11, d_12, d_13);
	l_21 = RETURN(l_18, null);
}
--EXPECT--
void test(double arg_1, double arg_2, double arg_3, double arg_4, double arg_5, bool cond)
{
	double d_1 = arg_1;
	double d_2 = arg_2;
	double d_3 = arg_3;
	double d_4 = arg_4;
	double d_5 = arg_5;
	bool d_6 = cond;
	double tmp_d;
bb2:
	if (!d_6) goto bb4;
	d_3 = d_2;
	d_2 = d_1;
	tmp_d = d_4;
	d_4 = d_5;
	d_5 = tmp_d;
	goto bb2;
bb4:
	printf("%g %g %g %g %g\n", d_1, d_2, d_3, d_4, d_5);
	return;
}
